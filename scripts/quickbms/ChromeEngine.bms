# Dead Island and Dying Light
# For csb and spb (fmod).
# Some other formats are supported too.

get EXT extension
if EXT == "csb"
    # http://zenhax.com/viewtopic.php?f=6&t=587
    goto 0x40
    get INFOSIZE long
    get FILES long
    get DUMMY long
    for i = 1 <= FILES
        getdstring NAME 0x40
        get OFFSET long
        get SIZE long
        get DUMMY long   
        get DUMMY long
        get DUMMY long
        get DUMMY long
        string NAME += "."  # auto-guess
        log NAME OFFSET SIZE
        set CHECK OFFSET
        math CHECK += SIZE
    next i
elif EXT == "spb"
    get MAJ long
    get MIN long
    get FILES long
    get ARCHIVE_SIZE long
    for i = 0 < FILES
        get ZERO long
        get OFFSET long
        get SIZE long
        get ZERO long
        getdstring NAME 0x40
        log NAME OFFSET SIZE
    next i
elif EXT == "rpack" or EXT == "rpacz"
    idstring "RP6L"
    get MAGIC long    # 1
    get VERSION long    # 1
    get COMPRESSION_TYPE long
    get PARTS_COUNT long
    get SECTION_COUNT long
    get FILE_COUNT long
    get FILENAME_TABLE_SIZE long
    get FILENAME_TABLE_COUNT long # same as FILE_COUNT usually
    get BLOCK_SIZE long
    for i = 0 < FILE_COUNT
        get DUMMY long
        get OFFSET long
        get SIZE long
        get ZSIZE long
        get DUMMY long
        if ZSIZE == 0
            log "" OFFSET SIZE
        else
            clog "" OFFSET ZSIZE SIZE
        endif
    next i
endif
